CREATE DATABASE [CigarShop]

GO

USE [CigarShop]

GO

--Problem 1
CREATE TABLE Sizes(
	Id INT PRIMARY KEY IDENTITY,
	[Length] INT NOT NULL CHECK([Length] BETWEEN 10 AND 25),
	RingRange Decimal(18,2) NOT NULL CHECK(RingRange BETWEEN 1.5 AND 7.5)
)

CREATE TABLE Tastes(
	Id INT PRIMARY KEY IDENTITY,
	TasteType VARCHAR(20) NOT NULL,
	TasteStrength VARCHAR(15) NOT NULL,
	ImageURL NVARCHAR(100) NOT NULL
)

CREATE TABLE Brands(
	Id INT PRIMARY KEY IDENTITY,
	BrandName VARCHAR(30) UNIQUE NOT NULL,
	BrandDescription VARCHAR(max)
)

CREATE TABLE Cigars(
	Id INT PRIMARY KEY IDENTITY,
	CigarName VARCHAR(80) NOT NULL,
	BrandId INT NOT NULL FOREIGN KEY REFERENCES Brands(Id),
	TastId INT NOT NULL FOREIGN KEY REFERENCES Tastes(Id),
	SizeId INT NOT NULL FOREIGN KEY REFERENCES Sizes(Id),
	PriceForSingleCigar MONEY NOT NULL,
	ImageURL NVARCHAR(100) NOT NULL
)

CREATE TABLE Addresses(
	Id INT PRIMARY KEY IDENTITY,
	Town VARCHAR(30) NOT NULL,
	Country NVARCHAR(30) NOT NULL,
	Streat NVARCHAR(100) NOT NULL,
	ZIP VARCHAR(20) NOT NULL
)

CREATE TABLE Clients(
	Id INT PRIMARY KEY IDENTITY,
	FirstName NVARCHAR(30) NOT NULL,
	LastName NVARCHAR(30) NOT NULL,
	Email NVARCHAR(50) NOT NULL,
	AddressId INT NOT NULL FOREIGN KEY REFERENCES Addresses(Id)
)

CREATE TABLE ClientsCigars(
	ClientId INT NOT NULL FOREIGN KEY REFERENCES Clients(Id),
	CigarId INT NOT NULL FOREIGN KEY REFERENCES Cigars(Id),
	PRIMARY KEY(ClientId, CigarId)
)

--Problem 2
INSERT INTO Cigars(CigarName, BrandId, TastId, SizeId, PriceForSingleCigar, ImageURL)
VALUES
('COHIBA ROBUSTO', 9, 1, 5, 15.50, 'cohiba-robusto-stick_18.jpg'),
('COHIBA SIGLO I', 9, 1, 10, 410.00, 'cohiba-siglo-i-stick_12.jpg'),
('HOYO DE MONTERREY LE HOYO DU MAIRE', 14, 5, 11, 7.50, 'hoyo-du-maire-stick_17.jpg'),
('HOYO DE MONTERREY LE HOYO DE SAN JUAN', 14, 4, 15, 32.00, 'hoyo-de-san-juan-stick_20.jpg'),
('TRINIDAD COLONIALES', 2, 3, 8, 85.21, 'trinidad-coloniales-stick_30.jpg')

INSERT INTO Addresses(Town, Country, Streat, ZIP)
VALUES
('Sofia', 'Bulgaria', '18 Bul. Vasil levski', '1000'),
('Athens', 'Greece', '4342 McDonald Avenue', '10435'),
('Zagreb', 'Croatia', '4333 Lauren Drive', '10000')

--Problem 3
UPDATE Cigars
   SET PriceForSingleCigar = PriceForSingleCigar * 1.2
 WHERE TastId = 1

UPDATE Brands
   SET BrandDescription = 'New description'
 WHERE BrandDescription IS NULL

--Problem 4
DELETE FROM Clients
 WHERE AddressId IN (
         SELECT c.AddressId
           FROM Clients AS c
           JOIN Addresses AS a
             ON c.AddressId = a.Id
          WHERE a.Country LIKE 'c%'
		)

DELETE FROM Addresses
      WHERE Country LIKE 'c%'

--Problem 5
  SELECT CigarName,
  	     PriceForSingleCigar,
  	     ImageURL
    FROM Cigars
ORDER BY PriceForSingleCigar, CigarName DESC

--Problem 6
   SELECT c.Id,
       	  c.CigarName,
          c.PriceForSingleCigar,
       	  t.TasteType,
       	  t.TasteStrength
     FROM Cigars AS c
LEFT JOIN Tastes AS t
	   ON c.TastId = t.Id
    WHERE t.TasteType IN ('Earthy', 'Woody')
 ORDER BY c.PriceForSingleCigar DESC

--Problem 7 
   SELECT c.Id,
          CONCAT(c.FirstName, ' ', c.LastName) AS ClientName,
          Email
     FROM Clients AS c
LEFT JOIN ClientsCigars AS cl
       ON c.Id = cl.ClientId
    WHERE cl.CigarId IS NULL
 ORDER BY c.FirstName 

--Problem 8
SELECT TOP (5) CigarName,
               PriceForSingleCigar,
               ImageURL
          FROM Cigars AS c
     LEFT JOIN Sizes AS s
            ON c.SizeId = s.Id
         WHERE [Length] >= 12 AND 
		       (c.CigarName LIKE '%ci%' OR c.PriceForSingleCigar > 50) 
		       AND s.RingRange > 2.55
      ORDER BY c.CigarName, c.PriceForSingleCigar DESC

--Problem 9
   SELECT CONCAT(c.FirstName, ' ', c.LastName) AS [FullName],
		  a.Country,
		  a.ZIP,
		  CONCAT('$', MAX(ci.PriceForSingleCigar))
     FROM Clients AS c
LEFT JOIN Addresses AS a
       ON c.AddressId = a.Id
LEFT JOIN ClientsCigars AS cl
       ON c.Id = cl.ClientId
LEFT JOIN Cigars AS ci
       ON cl.CigarId = ci.Id
    WHERE ISNUMERIC(a.ZIP) = 1
 GROUP BY c.FirstName, c.LastName, a.Country, a.ZIP
 ORDER BY FullName

--Problem 10
  SELECT c.LastName,
		 AVG(s.[Length]) AS CiagrLength,
		 CEILING(AVG(s.RingRange)) AS CiagrRingRange
    FROM Clients AS c
    JOIN ClientsCigars AS cl
      ON c.Id = cl.ClientId
    JOIN Cigars AS ci
      ON cl.CigarId = ci.Id
    JOIN Sizes AS s
      ON ci.SizeId = s.Id
GROUP BY c.LastName
ORDER BY AVG(s.[Length]) DESC

GO

--Problem 11
CREATE FUNCTION udf_ClientWithCigars(@name NVARCHAR(30))
RETURNS INT
AS
BEGIN
	DECLARE @numberOfCigars INT = (
									                SELECT COUNT(cl.CigarId)
	                                FROM Clients AS c
	                                JOIN ClientsCigars AS cl ON c.Id = cl.ClientId
	                                WHERE c.FirstName LIKE @name
								  )
	RETURN @numberOfCigars
END

GO

--Problem 12
CREATE PROC usp_SearchByTaste @taste VARCHAR(20)
AS
BEGIN
	SELECT c.CigarName, 
		   CONCAT('$', c.PriceForSingleCigar) AS Price, 
		   t.TasteType, 
		   b.BrandName, 
		   CONCAT(s.[Length], ' cm') AS CigarLength, 
		   CONCAT(s.RingRange, ' cm') AS CigarRingRange
	FROM Cigars AS c
	JOIN Brands AS b ON c.BrandId = b.Id
	JOIN Tastes AS t ON c.TastId = t.Id
	JOIN Sizes AS s ON c.SizeId = s.Id
	WHERE t.TasteType LIKE @taste
	ORDER BY s.[Length], s.RingRange DESC
END
